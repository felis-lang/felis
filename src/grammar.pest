WHITESPACE = _{ " " | "\n" | "\t" }
COMMENT = _{ "//" ~ (!("\n") ~ ANY)* }

alpha = _{ 'a'..'z' | 'A'..'Z' }
digit = _{ '0'..'9' }

number = @{ digit+ }
string = @{ "\"" ~ (!("\"") ~ ANY)* ~ "\"" }
boolean = _{ tru | fls }
  tru = _{ "true" }
  fls = _{ "false" }

ident = @{ (alpha | "_") ~ (alpha | digit | "_")* }

main = _{ SOI ~ stmt* ~ EOI }

stmt = _{ stmt_valbind | stmt_expr /* | typebind */ }

stmt_expr = { expr ~ ";" }

stmt_valbind = { "let" ~ pattern ~ type_anno ~ "=" ~ expr ~ ";" }

pattern = _{ ident | patt_tuple }

patt_tuple = { "(" ~ pattern ~ ("," ~ pattern)* ~ ")" }

type_anno = { (":" ~ type)? }

val_atom = _{ prefix* ~ (number | string | boolean | func | ident | val_tuple) ~ suffix* }

val_tuple = { "(" ~ expr ~ ("," ~ expr)* ~ ")" }

expr = { val_atom ~ (val_infix ~ val_atom)* }

func = { "fn" ~ pattern ~ "->" ~ expr }

prefix = _{ neg }
  neg = { "-" }

suffix = _{ call }
  call = { val_tuple }

val_infix = _{ add | sub | pow | mul | div }
  add = { "+" }
  sub = { "-" }
  pow = { "**" }
  mul = { "*" }
  div = { "/" }

type = { type_atom ~ (type_infix ~ type_atom)* }

type_infix = _{ arrow }
  arrow = { "->" }

type_atom = { ident | type_tuple }

type_tuple = { "(" ~ type ~ ("," ~ type)* ~ ")" }
